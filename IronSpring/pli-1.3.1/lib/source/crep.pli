 /* _pli_CRep                                                        */
 /********************************************************************/
 /*           Iron Spring PL/I Runtime Library Version 0.5           */
 /*              Distributed under the Gnu LGPL License              */
 /*                                                                  */
 /*      Module:        _pli_CRep (PL/I runtime)                     */
 /*      Version:       1.0                                          */
 /*      Date:          Mar, 2007                                    */
 /*      Author:        Peter Flass                                  */
 /*                                                                  */
 /*      Entry Points:  _pli_CRep [REPEAT()/COPY() BIFs]             */
 /*                                                                  */
 /*      Function:      Implement REPEAT and COPY for                */
 /*                     character string expressions                 */
 /*                     longer than a single character.              */
 /*                                                                  */
 /*                     The compiler generates inline code for       */
 /*                     character REPEAT/COPY for a single           */
 /*                     character.                                   */
 /*                                                                  */
 /*      Dependencices: PL/I Locator/Descriptor formats.             */
 /*                                                                  */
 /*      Calling sequence:                                           */
 /*                     DCL CRep entry(pSrc,pDSrc,tTgt,pDTgt)        */
 /*                                options( LINKAGE(SYSTEM) );       */
 /*                                                                  */
 /*                     pTgt->Target string                          */
 /*                     pDTgt->Target Descriptor                     */
 /*                     pSrc->Source string                          */
 /*                     pDSrc->Source Descriptor                     */
 /*                                                                  */
 /*                     'Target' and 'Source' are both character-    */
 /*                     string expressions.                          */
 /*                                                                  */
 /*                     Compiled code guarantees that the target     */
 /*                     length is an exact multiple of the           */
 /*                     source length.  The target is always         */
 /*                     a nonvarying string.                         */
 /*                                                                  */
 /*                     Each address points to a character-string    */
 /*                     Locator/Descriptor.                          */
 /*                                                                  */
 /*      Exceptions:    No conditions should be raised.              */
 /*                                                                  */
 /*      To Do:                                                      */
 /*                                                                  */
 /*      Modifications:                                              */
 /*                                                                  */
 /********************************************************************/

 CRep: proc( pSrc, pDSrc, pTgt, pDTgt )
         options( LINKAGE(SYSTEM) )
         ext( '_pli_CRep' );

 /*-------------------------*/
 /* Parameters              */
 /*-------------------------*/
 dcl    (pSrc,pDSrc)         ptr;
 dcl    (pTgt,pDTgt)         ptr;

 /*-------------------------*/
 /* Automatic Data          */
 /*-------------------------*/
 dcl    (pT,pS)              ptr;      /* ->Src,Tgt Data    */
 dcl    (lSrc,lTgt)          fixed bin(31);

 /*-------------------------*/
 /* Prototypes              */
 /*-------------------------*/
 dcl     Varying_String      char(0)   varying   based;

 dcl    PLIMOVE              builtin;
 %page;
 /*-----------------------------------*/
 /* Get Addresses and Lengths         */
 /*-----------------------------------*/
 pT = pTgt;                            /* Address of Target          */
 lTgt = pDTgt->ST_len;                 /* Length of Target           */
 pS = pSrc;                            /* Address of Source          */
 if pDSrc->ST_id='10'x                 /* Is Source fixed-length?    */
 then lSrc=pDSrc->ST_len;              /* .. Yes, length from descr  */
 else do;                              /* .. No, length from prefix  */
   lSrc = length(pSrc->Varying_String);
   pS = pS + stg( sysnull()->Varying_String );
   end;

 if lSrc<=0 then return;               /* Zero-length source         */
 /* COMMENT: It requires several instructions to check the source    */
 /*          length for zero.  Assuming this is a fairly uncommon    */
 /*          occurrence, there's less overhead to check here.        */
 do while( lTgt>0 );                  /* Move copies of Source       */
   call PLIMOVE( pT, pS, lSrc );
   pT = pT+lSrc;
   lTgt = lTgt-lSrc;
   end; /* do while */

 return;

 %page;
 %include DESC;

 end CRep;
